PARSER_BEGIN(CLang)
public class CLang
{
    
}
PARSER_END(CLang)

SKIP : {
 " "
|  "\t"
|  "\n"
|  "\r"
|  <"//" (~["\n","\r"])* ("\n" | "\r" | "\r\n")>
|  <"/*" (~["*"])* "*" ("*" | ~["*","/"] (~["*"])* "*")* "/">
}

TOKEN : {
    <TYPE: <INT> | <CHAR> | <VOID> >     |
    <WHILE:  "while"  >                  |
    <RETURN: "return"  >                 |
    <FOR:    "for"  >                    |
    <IF:     "if"  >                     |
    <#INT:   "int" >                     |
    <#CHAR:  "char" >                    |
    <#VOID:  "void" >                    
}

TOKEN: {
    <LRBRACKET: "(" > |
    <RRBRACKET: ")" > |
    <COMMA:     "," > |
    <LCBRACKET: "{" > |
    <RCBRACKET: "}" > |
    <SEMICOLON: ";" > |
    <ASSIGN:    "=" > |
    <APSOTREPHE:"'" > |
    <EQ:       "==" > |
    <LT:       "<"  > |
    <LTE:      "<=" > |
    <GT:       ">"  > |
    <GTE:      ">=" > |
    <NOT:      "!"  > |
    <NEQ:      "!=" > |
    <AND:      "&&" > |
    <OR:       "||" > |
    <ADD:       "+" > |
    <SUB:       "-" > |
    <MUL:       "*" > |
    <DIV:       "/" > |
    <MOD:       "%" >  
}
   
TOKEN: {
    <  ID:<LETTER> (<LETTER> | <DIGIT> | <ZERO>)*>     |
    <NUMBER: <DIGIT> (<DIGIT> | <ZERO>)* | <ZERO> >                 |
    <CHAR_VALUE: <APSOTREPHE> (<LETTER> | <DIGIT>) <APSOTREPHE> >   | 
    <#LETTER: ["a"-"z","A"-"Z"] >                                   |
    <#DIGIT: ["1"-"9"] >                                            |
    <#ZERO: "0" >
}

void idExpressionDef() : {}
{
    <LRBRACKET>idExpressionDef()<RRBRACKET>
    | <ID>             
}

void constExpressionDef() : {}
{
    <ID>                                         |
    <NUMBER>                                    |
    <CHAR_VALUE>                                
}

void listExpressionDef()   : {}
{
    expressionDef() (<COMMA> expressionDef())*
}

void functionCall()   : {}
{
    idExpressionDef()<LRBRACKET>[listExpressionDef() ]<RRBRACKET>
}

void unaryExpressionDef() : {}
{                     
    LOOKAHEAD(functionCall() )functionCall()        |
    constExpressionDef()
}

void mulExpressionDef() : {}
{
     LOOKAHEAD(2 )<LRBRACKET>mulExpressionDef() <RRBRACKET>|
   unaryExpressionDef() [ (<MUL> | <DIV> | <MOD>) mulExpressionDef() ]
}

void addExpressionDef() : {}
{
    mulExpressionDef() [ (<ADD> | <SUB>) addExpressionDef() ]
}

void binaryBoolExpressionCompareDef() : {}
{
    addExpressionDef() [ binaryBoolOperatorCompareDef() binaryBoolExpressionCompareDef() ]
}

void binaryExpressionEqDef() : {}
{
    binaryBoolExpressionCompareDef() [ (<EQ> | <NEQ> ) binaryExpressionEqDef() ] 
}

void binaryBoolExpressionAndDef() : {}
{
    binaryExpressionEqDef() [ <AND> binaryBoolExpressionAndDef() ]
}

void binaryBoolExpressionOrDef() : {}
{
    binaryBoolExpressionAndDef() [ <OR> binaryBoolExpressionOrDef() ]
}

void binaryBoolOperatorCompareDef() : {}
{
    
    <LT> | <LTE> | <GT> | <GTE>
}

void boolExpressionDef() : {}
{
    <NOT>boolExpressionDef()   |
   
    LOOKAHEAD(2) <LRBRACKET>boolExpressionDef()<RRBRACKET>|
    binaryBoolExpressionOrDef()
}

void assignExpressionDef() : {}
{
   idExpressionDef()<ASSIGN> expressionDef()    
}

void expressionDef() : {}
{
    LOOKAHEAD(2) assignExpressionDef() |   
    boolExpressionDef()
}

void varAssignDefInInit() : {}
{
    <ASSIGN> expressionDef()
}
void listVarDefineDef() : {}
{
    <TYPE> idExpressionDef() [varAssignDefInInit()]
    (<COMMA>idExpressionDef() [varAssignDefInInit()])*
}

void varDefineDef() : {}
{
    <TYPE> idExpressionDef() [varAssignDefInInit()]
}

void StatementBlockDef() : {}
{
    <LCBRACKET> (StatementDef())* <RCBRACKET>
}

void paramDef() : {}
{
     <TYPE> idExpressionDef()
}

void paramListDef() : {}
{
    paramDef() (<COMMA> paramDef())*
}

void functionDef() : {}
{
    paramDef() <LRBRACKET> [paramListDef()] <RRBRACKET> StatementBlockDef()
}

void sourceCodeDef() : {}
{   
    LOOKAHEAD( varDefineDef() <SEMICOLON> ) varDefineDef() <SEMICOLON> | functionDef()
}

void Start() : {}
{
    (sourceCodeDef())*
}

void constExpressionOrBinaryDef() : {}
{
    constExpressionDef()                            |
    <NOT>     constExpressionOrBinaryDef()               | 
    <LRBRACKET>constExpressionOrBinaryDef() <RRBRACKET>    
}

void ReturnStatementDef() : {}
{
    <RETURN> [expressionDef()]
}

void WhileStatementDef() : {}
{
    <WHILE> <LRBRACKET> expressionDef()
    <RRBRACKET> (StatementBlockDef() | StatementDef())
}

void ForStatementDef() : {}
{
    <FOR>  <LRBRACKET> (varDefineDef()|
    LOOKAHEAD(assignExpressionDef())assignExpressionDef()|
   idExpressionDef() )
    (<COMMA>(LOOKAHEAD(assignExpressionDef())assignExpressionDef()|idExpressionDef() ))* 
    <SEMICOLON>boolExpressionDef()
    <SEMICOLON>listExpressionDef() 
    <RRBRACKET> (StatementBlockDef() | StatementDef())
}

void IfStatementDef() : {}
{
    <IF> <LRBRACKET> expressionDef() <RRBRACKET> (StatementBlockDef() | StatementDef())
}

void StatementDef() : {}
{
    varDefineDef()        <SEMICOLON>    |
    expressionDef()       <SEMICOLON>    |
    IfStatementDef()                     |
    ReturnStatementDef()  <SEMICOLON>    |
    ForStatementDef()                    |
    WhileStatementDef()
}






